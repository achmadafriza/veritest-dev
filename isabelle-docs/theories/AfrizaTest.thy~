(* Each theory file starts with a header like the one below *)
theory AfrizaTest
  imports
    Canonicalizations.Common
    Proofs.StampEvalThms
begin

(* Ignore this definition *)
definition isBoolean :: "IRExpr \<Rightarrow> bool" where
  "isBoolean e = (\<forall>m p cond. (([m,p] \<turnstile> e \<mapsto> cond) \<longrightarrow> (cond \<in> {IntVal 32 0, IntVal 32 1})))"


(* Use of the optimization keyword is restricted to within a phase *)
(* We start a phase and provide a pre-made termination measure, size *)
phase ConditionalNode
  terminating size
begin

(* An optimization that nitpick can show is wrong *)
optimization Wrong: "x + y \<longmapsto> x - y"
  nitpick

  (* We have sorry at the end so the rest of the file can be parsed *)
  (* Otherwise Isabelle will not allow the unfinished obligation introduced by the optimization command to continue *)
  sorry

(* An optimization that requires no further proof *)
optimization Auto: "(true ? x : y) \<longmapsto> x"
  .

(* An optimization that sledgehammer can produce *)
(* The sledgehammer command is normally removed after the tactic is included in the theory file *)
optimization SemiAuto: "(c ? true : false) \<longmapsto> c
                                          when isBoolean c"
  sledgehammer
  using empty_iff insert_iff isBoolean_def by fastforce

optimization SemiAutoNitpick: "(c ? true : false) \<longmapsto> c
                                          when isBoolean c"
  nitpick
  sorry


(* An optimization that requires a rather nasty manual proof *)
(* Understanding this proof not required for your project! *)
optimization Manual: "((!e) ? x : y) \<longmapsto> (e ? y : x)"
  apply simp apply (rule allI; rule allI; rule allI; rule impI)
  subgoal premises p for m p v
  proof -
    obtain ev where ev: "[m,p] \<turnstile> e \<mapsto> ev"
      using p by blast
    then have notEv: "[m,p] \<turnstile> exp[!e] \<mapsto> intval_logic_negation ev"
      using evalDet p by fastforce
    have inverse: "val_to_bool ev = (\<not>(val_to_bool (intval_logic_negation ev)))"
      by (smt (verit) eval_bits_1_64 intval_logic_negation.elims logic_negate_def new_int.simps notEv of_bool_eq(2) one_neq_zero take_bit_of_0 take_bit_of_1 unfold_unary val_to_bool.simps(1))
    obtain xv where xvdef: "[m, p] \<turnstile> x \<mapsto> xv"
      using p by auto
    obtain yv where yvdef: "[m, p] \<turnstile> y \<mapsto> yv"
      using p by auto
    then show ?thesis
    proof (cases "val_to_bool ev")
      case True
      then have "[m, p] \<turnstile> exp[(!e) ? x : y] \<mapsto> yv"
        using ConditionalExpr evaltree_not_undef inverse notEv xvdef yvdef by auto
      also have "[m, p] \<turnstile> exp[e ? y : x] \<mapsto> yv"
        using True calculation ev evaltree_not_undef yvdef by auto
      ultimately show ?thesis
        by (metis evalDet p)
    next
      case False
      then have "[m, p] \<turnstile> exp[(!e) ? x : y] \<mapsto> xv"
        using ConditionalExpr evaltree_not_undef inverse notEv xvdef yvdef by auto
      also have "[m, p] \<turnstile> exp[e ? y : x] \<mapsto> xv"
        using ConditionalExpr False ev evaltree_not_undef xvdef yvdef by force
      ultimately show ?thesis
        by (metis evalDet p)
    qed
  qed
  done

end

end
